apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'maven-publish'

sourceCompatibility = 11
targetCompatibility = 11

group 'com.github.UstadMobile'
version "0.1c-snapshot"


repositories {
    mavenCentral()
    maven {
        url "https://devserver3.ustadmobile.com/maven2/"
    }
}


application {
    mainClass = 'net.luminis.quic.run.KwikCli'
    applicationName = "kwik"
}

distributions {
    main {
        distributionBaseName = 'kwik'
    }
}


dependencies {
    def junitVersion = "5.9.2"

    implementation "com.github.UstadMobile:agent15:0.1a"

    // https://mvnrepository.com/artifact/at.favre.lib/hkdf
    implementation group: 'at.favre.lib', name: 'hkdf', version: '1.0.1'

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'

    implementation 'org.glassfish:javax.json:1.1.4'

    testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-params:$junitVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testImplementation("org.mockito:mockito-core:4.11.0")
    testImplementation("org.assertj:assertj-core:3.24.2")
}

sourceSets {
    main {
        resources {
            srcDir "${buildDir}/generated"
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed"
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }

    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
        }
    }
}


task determineVersion {
    doLast {
        def proc = "git describe --always --dirty".execute()
        proc.waitFor()
        if (proc.exitValue()) {
            logger.quiet("Building development version.")
        } else {
            def versionTag = proc.in.readLines()[0]
            logger.quiet("Building version ${versionTag}.")

            ext.versionDir = new File("${buildDir}", "generated/net/luminis/quic")
            versionDir.mkdirs()

            new File(versionDir, "version.properties").text = versionTag
        }
    }
}

compileJava.dependsOn (determineVersion, processResources)

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "kwik"

            from components.java
        }
    }
}

